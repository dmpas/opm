///////////////////////////////////////////////////////////////////////////////////////////////////
// Прикладной интерфейс

Процедура ЗарегистрироватьКоманду(Знач ИмяКоманды, Знач Парсер) Экспорт
	
	ОписаниеКоманды = Парсер.ОписаниеКоманды(ИмяКоманды, "Вывести список пакетов");
	Парсер.ДобавитьПараметрФлагКоманды(ОписаниеКоманды, "-q", """Тихий"" режим вывода без лишних сообщений.");
	Парсер.ДобавитьПараметрФлагКоманды(ОписаниеКоманды, "-remote", "Вывести список пакетов в хабе");
	Парсер.ДобавитьКоманду(ОписаниеКоманды);

КонецПроцедуры

// Выполняет логику команды
// 
// Параметры:
//   ПараметрыКоманды - Соответствие ключей командной строки и их значений
//
Функция ВыполнитьКоманду(Знач ПараметрыКоманды) Экспорт
    
	ВывестиСписокПакетов(ПараметрыКоманды);
	Возврат 0;

КонецФункции


Процедура ВывестиСписокПакетов(Знач ЗначенияПараметров)
	
	ТихийРежим = ЗначенияПараметров["-q"];
	
	Если ЗначенияПараметров["-remote"] Тогда
		ТекстСообщения  = СтрШаблон("Пакеты в хабе:");		
		МенеджерПолучения = Новый МенеджерПолученияПакетов();
		УстановленныеПакеты = МенеджерПолучения.ПолучитьДоступныеПакеты();
	Иначе	
		
		ПутьККаталогуПакетов = ПолучитьЗначениеСистемнойНастройки("lib.system");
		ТекстСообщения = СтрШаблон("Пакеты в системном каталоге библиотек <%1>:", ПутьККаталогуПакетов);		
		
		КэшУстановленныхПакетов = Новый КэшУстановленныхПакетов(ПутьККаталогуПакетов);
		УстановленныеПакеты = КэшУстановленныхПакетов.ПолучитьУстановленныеПакеты();
	КонецЕсли;
	
	Если НЕ ТихийРежим Тогда
		Сообщить(ТекстСообщения);
	КонецЕсли;
	
	Для Каждого УстановленныйПакет Из УстановленныеПакеты Цикл
		СтрокаСообщения = УстановленныйПакет.Ключ;
		Если ТипЗнч(УстановленныйПакет.Значение) = Тип("Структура") Тогда
			ОписаниеПакета = УстановленныйПакет.Значение.МетаданныеПакета;
			Если ТипЗнч(ОписаниеПакета) = Тип("Булево") Тогда
				// @deprecated
			Иначе
				СтрокаСообщения = СтрокаСообщения + "@" + ОписаниеПакета.Свойства().Версия;
			КонецЕсли;		
		КонецЕсли;
		Сообщить(СтрокаСообщения);
	КонецЦикла;
	
КонецПроцедуры